group 'at.phatbl'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.4-2'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

task removeBatchFile(type: Delete) { delete 'gradlew.bat' }

wrapper {
    gradleVersion = '4.1'
    distributionType = 'ALL'
    finalizedBy removeBatchFile
}

apply plugin: 'kotlin2js'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
}

kotlin.experimental.coroutines 'enable'

/**
 * @see https://kotlinlang.org/docs/reference/using-gradle.html#attributes-specific-for-js
 */
compileKotlin2Js.kotlinOptions {
    outputFile = "build/js/module.js"
    moduleKind = "commonjs"
    metaInfo = true
    sourceMap = true
    // Generate ES6 (ECMAScript 6, 2015) compliant .js files.
    target = "v6"
}

// Copy source set dependencies copies to the build output directory.
// https://gist.github.com/ElFeesho/617cc58979b1172c7add31c2986fd8c0
build.doLast() {
    // Augmented from https://github.com/bascan/kotlin-js-example/blob/master/build.gradle

    def jsFileFilter = { fileTreeElement ->
        def path = fileTreeElement.path
        path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
    }

    configurations.compile.each { File file ->
        copy {
            includeEmptyDirs = false
            from zipTree(file.absolutePath)
            into "${project.buildDir}/classes/main/lib"
            include(jsFileFilter)
        }
    }
    configurations.testCompile.each { File file ->
        copy {
            includeEmptyDirs = false
            from zipTree(file.absolutePath)
            into "${project.buildDir}/classes/test/lib"
            include(jsFileFilter)
        }
    }
}

/* -----[ Custom Tasks ]--------------------------------------------------------------------------------------------- */

/**
 * Generates definitions in Kotlin from the Realm NPM package. Run manually as the generated files need edits to compile
 * successfully.
 */
task generateKotlinHeadersForRealmJS(type: Exec) {
    commandLine "node_modules/ts2kt/ts2kt.js -d src/main/kotlin node_modules/realm/lib/index.d.ts".tokenize()
}

/**
 * Launches node app through npm.
 */
task run(type: Exec) {
    dependsOn build
    commandLine "npm start".tokenize()
}
