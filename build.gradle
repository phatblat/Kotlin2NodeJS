group 'at.phatbl'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.4'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin2js'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
}

compileKotlin2Js.kotlinOptions {
    outputFile = "node/index.js"
    moduleKind = "commonjs"
    metaInfo = true
    sourceMap = true
}

task removeBatchFile(type: Delete) { delete 'gradlew.bat' }

wrapper {
    gradleVersion = '4.1'
    distributionType = 'ALL'
    finalizedBy removeBatchFile
}

/* -----[ Custom Tasks ]--------------------------------------------------------------------------------------------- */

/**
 * Generates definitions in Kotlin from the Realm NPM package. Run manually as the generated files need edits to compile
 * successfully.
 */
task generateKotlinHeadersForRealmJS(type: Exec) {
    commandLine "ts2kt -d src/main/kotlin node_modules/realm/lib/index.d.ts".tokenize()
}
